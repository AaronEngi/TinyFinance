plugins {
    id("edu.sc.seis.launch4j")
    id("org.openjfx.javafxplugin")
    id("application")   // creates a task to run the full application
    id("distribution")
}

description = "jGnashFx BootLoader"

application {
    mainClassName = "jGnash"
}

javafx {
    version = javaFXVersion
    modules("javafx.base", "javafx.controls", "javafx.fxml", "javafx.web", "javafx.swing",
            "javafx.graphics", "javafx.media")
}

dependencies {
    implementation(project(":jgnash-fx"))
    implementation(project(":jgnash-resources"))
    implementation(project(":jgnash-core"))
    implementation("info.picocli:picocli:$picocliVersion")
}

launch4j {
    copyright = "Copyright (C) 2001-2019 Craig Cavanaugh"
    mainClassName = "jGnash"
    outfile = "jGnash.exe"
    dontWrapJar = true
    icon = "../../../jgnash-fx/gnome-money.ico"
    downloadUrl = "https://www.azul.com/downloads/zulu/"
    bundledJrePath = "%JAVA_HOME%"
    // work around for not being able to find installed jre, https://sourceforge.net/p/launch4j/bugs/197/
    jreMinVersion = "11"
}

task createStartScripts(type: CreateStartScripts) {
    outputDir = file("../../")
}

// call launch4j prior to creating the zip file
distZip.dependsOn(createExe)

distributions {
    main {
        distributionBaseName = "jGnash"

        contents {
            from { "../jgnash-manual/src/Manual.pdf" }
            from { "../changelog.adoc" }
            from { "build/launch4j/jGnash.exe" }
            from { "../README.html" }
            from { "../README.adoc" }
            from { "../jGnash" }
            exclude("**/*-linux*")  // excludes linux specific JavaFx modules from cross platform zip
            exclude("**/*-win*")    // excludes windows specific JavaFx modules from cross platform zip
            exclude("**/*-mac*")    // excludes mac specific JavaFx modules from cross platform zip
        }
    }
}

jar {
    // Keep jar clean:
    exclude "META-INF/*.SF", "META-INF/*.DSA", "META-INF/*.RSA", "META-INF/*.MF"

    manifest {
        attributes "Main-Class": "jGnash",
                "Class-Path": configurations.runtimeClasspath.files.collect { "lib/$it.name" }.join(" ")
    }
}